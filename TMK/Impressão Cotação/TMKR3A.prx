#Include 'Protheus.ch'
#include 'colors.ch'
#Include 'Font.ch'
#Include 'TopConn.ch'

User Function TMKR3A()
	Local cTitulo   		:= "Cotação"	
	
	Private oFont1 			:= TFont():New("Arial",09,14,		,.T.,,,,,.F.) //Parametros TFonte ("Tipo Fonte", ,Tamanho Fonte , , ,Italico (.T./.F.))
	Private oFont2  		:= TFont():New("Arial",07,08,		,.F.,,,,,.F.) // Normal
	Private oFont3  		:= TFont():New("Arial",09,10,.T.	,.T.,,,,,.F.) // Negrito
	Private oFont4  		:= TFont():New("Arial",09,14,.T.	,.T.,,,,,.T.) // Negrito - Sublinhado
	Private oFont5  		:= TFont():New("Arial",09,10,   	,.F.,,,,,.F.) // Negrito
	Private oPrint    
	
	Private nTotPed		:= 0
	Private cTES			:= ""
	
	// Instânciação do Objeto OPrint
	oPrint := TMSPrinter():New()
	
	//Inicia uma Nova Pagina para Impressao
	oPrint:StartPage()
		
	//Define o modo de Impressao como Retrato
	oPrint:SetPortrait()  //SetLandscape -> Para definir como modo Paisagem
	IMPCAB()	//Funcao para impressao do Cabecalho
	IMPCLI()	//Funcao para impressao dos dados do Cliente
	IMPPRO()	//Funcao para impressao dos Detalhes do Produto	
	IMPROD()  //Funcao para impressao do Rodapé
	   	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ MsDialog oDlg: exibe as opcoes para tratativa do relatorio.      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
	Define MsDialog oDlg Title "Cotação" From 0, 0 To 090, 500 Pixel
	Define Font oBold Name "Arial" Size 0, -13 Bold
	@ 000, 000 Bitmap oBmp ResName "LOGIN" Of oDlg Size 30, 120 NoBorder When .F. Pixel
	@ 003, 040 Say cTitulo Font oBold Pixel
	@ 014, 030 To 016, 400 Label '' Of oDlg  Pixel
	@ 020, 040 Button "Configurar" 	Size 40, 13 Pixel Of oDlg Action oPrint:Setup()
	@ 020, 082 Button "Imprimir"   	Size 40, 13 Pixel Of oDlg Action oPrint:Print()
	@ 020, 124 Button "Visualizar" 	Size 40, 13 Pixel Of oDlg Action oPrint:Preview() 	
	@ 020, 208 Button "Sair"       	Size 40, 13 Pixel Of oDlg Action oDlg:End()
	Activate MsDialog oDlg Centered


Return


Static Function IMPCAB(cNPc, cEMISSAO)

	Local nTopo			:= 0000  // Controle de Linhas
	Local nInicio		:= 0000  // Indica a posição da primeira coluna 
	Local cEmail 		:= allTrim(getMV("ZZ_EMAILCA"))
	Local cLogo			:= AllTrim(GetMV("ZZ_LOGOEMP")) //Imagem dentro do SYSTEM
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Retorna o tamanho Horizontal e Vertical da pagina.                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nVertSize := oPrint:nVertSize()
	nHorzSize := oPrint:nHorzSize()  
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Impressao do cabeçalho do Relatorio.															³
	//³Obs.: O Logo pode ser colocado na pasta System do Protheus, no caso só referencia o nome.    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
	// oPrint:SayBitmap(Coluna, Linha, Caminho, Largura, Altura)
	oPrint:SayBitmap(nTopo + 0050, nInicio + 0040,cLogo, 410, 185)
	
	dbSelectArea("SM0")
	SM0->(dbseek(cEmPant + cFilant))
	
	// oPrint:Say(Coluna, Linha, Texto, Fonte, Num de Caracteres, , ,Alinhamento - 0=Left, 1=Right e 2=Center)
	oPrint:Say(nTopo + 0050, nInicio + 2360,RTrim(SM0->M0_NOMECOM)												   		,oFont1,100,,,1)
	oPrint:Say(nTopo + 0100, nInicio + 2360, AllTrim(SM0->M0_ENDENT) + " " + AllTrim(SM0->M0_CIDENT) + "/" + AllTrim(SM0->M0_ESTENT) + " " + LEFT(AllTrim(SM0->M0_CEPENT),5) + "-" + RIGHT(AllTrim(SM0->M0_CEPENT),3)		,oFont5,100,,,1)
	oPrint:Say(nTopo + 0150, nInicio + 2360, "E-mail: " + Alltrim(cEmail)								   			,oFont3,100,,,1)//							   			,oFont3,100,,,1)
	oPrint:Say(nTopo + 0200, nInicio + 2360, "Fone: " + AllTrim(SM0->M0_TEL)	   		,oFont5,100,,,1)
	oPrint:Say(nTopo + 0250, nInicio + 2360, "CNPJ: " + AllTrim(Transform(SM0->M0_CGC, "@R 99.999.999/9999-99")) + " - IE: " +  AllTrim(SM0->M0_INSC)	,oFont5,100,,,1)
	
	// oPrint:Line(Coluna, Linha, Coluna, Linha)
	oPrint:Line(nTopo + 0310, nInicio + 0040,nTopo + 0310, nInicio + 2360)
	oPrint:Line(nTopo + 0313, nInicio + 0040,nTopo + 0313, nInicio + 2360)
	oPrint:Line(nTopo + 0410, nInicio + 0040,nTopo + 0410, nInicio + 2360)
	oPrint:Line(nTopo + 0413, nInicio + 0040,nTopo + 0413, nInicio + 2360)

	oPrint:Say(nTopo + 0335	, nInicio + 0040, "Cotação N° " + SUA->UA_NUM		,oFont1,100,,,0)
	oPrint:Say(nTopo + 0335	, nInicio + 2360, "Data: " + DTOC(dDataBase)			,oFont1,100,,,1)

Return

Static Function IMPCLI()
	Local nTopo		:= 0000  // Controle de Linhas
	Local nInicio		:= 0000  // Indica a posicao da primeira coluna
	Local cCGC			:= ""
	Local cCod			:= ""
	Local cNome		:= ""
	Local cEmail		:= ""
	Local cEnd			:= ""
	Local cMun			:= ""
	Local cEst			:= ""
	Local cDDD			:= ""
	Local cTel			:= ""
	Local cBairro		:= ""
	Local cCEP			:= ""
	Local cInscri		:= ""
	
	If SUA->UA_PROSPEC
		dbSelectArea("SUS")
		dbSetOrder(1)//US_FILIAL+US_COD+US_LOJA
		dbSeek(xFilial("SUS")+SUA->UA_CLIENTE+SUA->UA_LOJA)
		cCGC			:= SUS->US_CGC
		cCod			:= SUS->US_COD
		cNome			:= SUS->US_NOME	
		cEmail			:= SUS->US_EMAIL
		cEnd			:= SUS->US_END
		cMun			:= SUS->US_MUN
		cEst			:= SUS->US_EST
		cDDD			:= SUS->US_DDD
		cTel			:= SUS->US_TEL
		cBairro		:= SUS->US_BAIRRO	
		cCEP			:= SUS->US_CEP
		cInscri		:= SUS->US_INSCR
	Else
		dbSelectArea("SA1")
		dbSetOrder(1)//A1_FILIAL+A1_COD+A1_LOJA
		dbSeek(xFilial("SA1")+SUA->UA_CLIENTE+SUA->UA_LOJA)
		cCGC			:= SA1->A1_CGC
		cCod			:= SA1->A1_COD
		cNome			:= SA1->A1_NOME	
		cEmail			:= SA1->A1_EMAIL
		cEnd			:= SA1->A1_END
		cMun			:= SA1->A1_MUN
		cEst			:= SA1->A1_EST
		cDDD			:= SA1->A1_DDD
		cTel			:= SA1->A1_TEL
		cBairro		:= SA1->A1_BAIRRO	
		cCEP			:= SA1->A1_CEP
		cInscri		:= SA1->A1_INSCR
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Impressão da Estrutura dos Dados do Cliente.                                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
	// Texto a Esquerda
	oPrint:Say(nTopo + 0430	, nInicio + 0040, "Cliente" 	,oFont3,100,,,0)
	oPrint:Say(nTopo + 0480	, nInicio + 0040, "E-mail"		,oFont3,100,,,0)
	oPrint:Say(nTopo + 0530	, nInicio + 0040, "Endereço"	,oFont3,100,,,0)
	oPrint:Say(nTopo + 0580	, nInicio + 0040, "Cidade"	  	,oFont3,100,,,0)
	oPrint:Say(nTopo + 0630	, nInicio + 0040, "TEL"  		,oFont3,100,,,0)
	If LEN(ALLTRIM(cCGC)) > 11
		oPrint:Say(nTopo + 0680	, nInicio + 0040, "CNPJ"  		,oFont3,100,,,0)
	Else
		oPrint:Say(nTopo + 0680	, nInicio + 0040, "CPF"  		,oFont3,100,,,0)
	EndIF
	
	//Texto Centralizado	
	oPrint:Say(nTopo + 0530	,Mm2Pix(oPrint, oPrint:nHorzSize()) / 2,"Bairro" 			,oFont3,100,,,0)
	oPrint:Say(nTopo + 0580	,Mm2Pix(oPrint, oPrint:nHorzSize()) / 2,"CEP" 				,oFont3,100,,,0)
	oPrint:Say(nTopo + 0630	,Mm2Pix(oPrint, oPrint:nHorzSize()) / 2,"IE"				,oFont3,100,,,0)

	// Texto a Esquerda
	oPrint:Say(nTopo + 0430	, nInicio + 0240, ": " + cCod + " - " + cNome		,oFont5,100,,,0)
	oPrint:Say(nTopo + 0480	, nInicio + 0240, ": " + cEmail		,oFont5,100,,,0)
	oPrint:Say(nTopo + 0530	, nInicio + 0240, ": " + cEnd			,oFont5,100,,,0)
	oPrint:Say(nTopo + 0580	, nInicio + 0240, ": " + AllTrim(cMun)+"/"+cEst	   		,oFont5,100,,,0)
	oPrint:Say(nTopo + 0630	, nInicio + 0240, ": (" + cDDD + ") " + Transform(cTel,"@r 9999-9999") ,oFont5,100,,,0)
	If LEN(ALLTRIM(cCGC)) > 11
		oPrint:Say(nTopo + 0680	, nInicio + 0240, ": " + AllTrim(Transform(cCGC,"@R 99.999.999/9999-99")) ,oFont5,100,,,0)
    Else
		oPrint:Say(nTopo + 0680	, nInicio + 0240, ": " + AllTrim(Transform(cCGC,"@R 999.999.999-99")) ,oFont5,100,,,0)
	EndIf	
	
	//Texto Centralizado	
	oPrint:Say(nTopo + 0530	,Mm2Pix(oPrint, oPrint:nHorzSize()) / 2 + 0150,": " + cBairro										,oFont5,100,,,0)
	oPrint:Say(nTopo + 0580	,Mm2Pix(oPrint, oPrint:nHorzSize()) / 2 + 0150,": " + LEFT(cCEP,5) + "-" + RIGHT(cCEP,3)	,oFont5,100,,,0)	
 	oPrint:Say(nTopo + 0630	,Mm2Pix(oPrint, oPrint:nHorzSize()) / 2 + 0150, ": " + AllTrim(cInscri) ,oFont5,100,,,0)   
	
	dbSelectArea("SU5")
	dbSetOrder(1)//U5_FILIAL+U5_CODCONT
	dbSeek(xFilial("SU5")+SUA->UA_CODCONT)
	oPrint:Say(nTopo + 0750	, nInicio + 0040, "Contato" 	,oFont3,100,,,0)
	oPrint:Say(nTopo + 0800	, nInicio + 0040, "E-mail"		,oFont3,100,,,0)
	oPrint:Say(nTopo + 0750	, nInicio + 0240, ": " + SU5->U5_CONTAT	,oFont5,100,,,0)
	oPrint:Say(nTopo + 0800	, nInicio + 0240, ": " + SU5->U5_EMAIL		,oFont5,100,,,0)
	
	oPrint:Say(nTopo + 0430	, nInicio + 0240, ": " + cCod + " - " + cNome		,oFont5,100,,,0)
	
	oPrint:Say(nTopo + 0430	, 1750, "Setor"		,oFont3,100,,,0)
	oPrint:Say(nTopo + 0430	, 2200, ": " + SUA->UA_VEND,oFont5,100,,,0)
	oPrint:Say(nTopo + 0480	, 1750, "Hora Impressão"		,oFont3,100,,,0)
	oPrint:Say(nTopo + 0480	, 2200, ": " + time(),oFont5,100,,,0)

Static Function IMPPRO()
	Local cQuery		:= ""
	Local nTopo		:= 0950  // Controle de Linhas
	Local nInicio	 	:= 0000  // Indica a posição da primeira coluna	
	Local nTamDesc	:= 0
	Local nPulo		:= 0050    
	Local nTotPed		:= 0	
	Local cTipo		:= ""
	Local nItem		:= 0
	
	If SUA->UA_PROSPEC
		cTipo := Posicione("SUS",1,xFilial("SUS") + SUA->UA_CLIENTE + SUA->UA_LOJA,"US_TIPO")
	Else		
		cTipo := Posicione("SA1",1,xFilial("SA1") + SUA->UA_CLIENTE + SUA->UA_LOJA,"A1_TIPO")
	Endif	
	
	MaFisClear()
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Inicializa os dados do cliente na FUNCAO FISCAL³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	MaFisIni(SUA->UA_CLIENTE,;							// 1-Codigo Cliente/Fornecedor
			 SUA->UA_LOJA,;								// 2-Loja do Cliente/Fornecedor
			 "C",;											// 3-C:Cliente , F:Fornecedor
			 "N",;											// 4-Tipo da NF
			 cTipo,;										// 5-Tipo do Cliente/Fornecedor
			 Nil,;											// 6-Relacao de Impostos que suportados no arquivo
			 Nil,;											// 7-Tipo de complemento
			 Nil,;											// 8-Permite Incluir Impostos no Rodape .T./.F.
			 "SB1",;										// 9-Alias do Cadastro de Produtos - ("SBI" P/ Front Loja)
			 "MATA461",;									// 10-Nome da rotina que esta utilizando a funcao
			 Nil,;											// 11-Tipo de documento
			 Nil,;  										// 12-Especie do documento 
		    IIF(SUA->UA_PROSPEC,SUA->UA_CLIENTE+SUA->UA_LOJA,""))// 13- Codigo e Loja do Prospect   

	cQuery := "SELECT * "+CRLF
	cQuery += "FROM " +CRLF
	cQuery +=		RetSqlName("SUB") + " SUB "+CRLF
	cQuery += "INNER JOIN "+CRLF
	cQuery +=    RetSqlName("SB1") + " AS SB1 ON B1_FILIAL = '" + xFilial("SB1") + "' AND UB_PRODUTO = B1_COD AND SB1.D_E_L_E_T_ = ' ' "+CRLF
	cQuery += "WHERE "+CRLF
	cQuery += "	UB_FILIAL = '" + xFilial("SUB") + "' "+CRLF
	cQuery += "	AND UB_NUM = '"+SUA->UA_NUM+"' "+CRLF
	cQuery += "	AND SUB.D_E_L_E_T_ = ' ' "+CRLF
		
	TCQUERY cQuery NEW ALIAS "cAlias"
	
	TCSETFIELD("cAlias","UB_DTENTRE","D",08,00)
	
	    
	oPrint:Box(nTopo, nInicio + 0040, nTopo + 0050, nInicio + 2360)
	
	//Linhas Verticais
	oPrint:Line(nTopo,nInicio + 0040,nTopo + 0050,nInicio + 0040)//Item
	oPrint:Line(nTopo,nInicio + 0150,nTopo + 0050,nInicio + 0150)//Produto
	oPrint:Line(nTopo,nInicio + 0450,nTopo + 0050,nInicio + 0450)//NCM
	oPrint:Line(nTopo,nInicio + 0670,nTopo + 0050,nInicio + 0670)//Descri
	oPrint:Line(nTopo,nInicio + 1180,nTopo + 0050,nInicio + 1180)//UM
	oPrint:Line(nTopo,nInicio + 1280,nTopo + 0050,nInicio + 1280)//Qtd
	oPrint:Line(nTopo,nInicio + 1420,nTopo + 0050,nInicio + 1420)//Prc Unit
	oPrint:Line(nTopo,nInicio + 1620,nTopo + 0050,nInicio + 1620)//Prc Total
	oPrint:Line(nTopo,nInicio + 1870,nTopo + 0050,nInicio + 1870)//IPI
	oPrint:Line(nTopo,nInicio + 1960,nTopo + 0050,nInicio + 1960)//ICMS
	oPrint:Line(nTopo,nInicio + 2080,nTopo + 0050,nInicio + 2080)//Dt Entreg
	
	// Texto    
	oPrint:Say(nTopo + 0007,nInicio + 0060,"Item"				   		,oFont3,100,,,0)
	oPrint:Say(nTopo + 0007,nInicio + 0170,"Produto"					,oFont3,100,,,0)
	oPrint:Say(nTopo + 0007,nInicio + 0470,"NCM"					,oFont3,100,,,0)
	oPrint:Say(nTopo + 0007,nInicio + 0690,"Descrição do Produto"		,oFont3,100,,,0)
	oPrint:Say(nTopo + 0007,nInicio + 1200,"UM"							,oFont3,100,,,0)
	oPrint:Say(nTopo + 0007,nInicio + 1300,"Qtd."						,oFont3,100,,,0)
	oPrint:Say(nTopo + 0007,nInicio + 1440,"Prc Unit."					,oFont3,100,,,0)
	oPrint:Say(nTopo + 0007,nInicio + 1640,"Prc Total"					,oFont3,100,,,0)
	oPrint:Say(nTopo + 0007,nInicio + 1890,"IPI"	  					,oFont3,100,,,0)
	oPrint:Say(nTopo + 0007,nInicio + 1980,"ICMS"	  					,oFont3,100,,,0)
	oPrint:Say(nTopo + 0007,nInicio + 2100,"Data Entrega"				,oFont3,100,,,0)
	nTopo += 0050 
	cAlias->(dbgotop()) 
	nTotPed := 0	
	While cAlias->(!Eof())
		cTES := cAlias->UB_TES
		nItem ++
		
		MaFisAdd(	cAlias->UB_PRODUTO,;
					cAlias->UB_TES,;
					cAlias->UB_QUANT,;
					cAlias->UB_VRUNIT,;
					0,;
					"",;
					"",;
					0,;
					0,;
					0,;
					0,;
					0,;
					cAlias->UB_VLRITEM,;
					0,;
					0,;
					0)
		nPulo := 0050
		If nTopo >= 2900
			oPrint:Line(nTopo + 0040,nInicio + 0040,nTopo + 0040,nInicio + 2360) 		 
			//IMPROD()	
			oPrint:EndPage()		
			//Inicia uma Nova Pagina para Impressao
			oPrint:StartPage()			
			//Define o modo de Impressão como Retrato
			oPrint:SetPortrait()   //SetLandscape -> Para definir como modo Paisagem			
			//Funcao para impressao do Cabecalho
			IMPCAB()												
			nTopo := 0450	
			oPrint:Box(nTopo, nInicio + 0040, nTopo + 0050, nInicio + 2360)
	
			//Linhas Verticais
			oPrint:Line(nTopo,nInicio + 0040,nTopo + 0050,nInicio + 0040)//Item
			oPrint:Line(nTopo,nInicio + 0150,nTopo + 0050,nInicio + 0150)//Produto
			oPrint:Line(nTopo,nInicio + 0450,nTopo + 0050,nInicio + 0450)//NCM
			oPrint:Line(nTopo,nInicio + 0670,nTopo + 0050,nInicio + 0670)//Descri
			oPrint:Line(nTopo,nInicio + 1180,nTopo + 0050,nInicio + 1180)//UM
			oPrint:Line(nTopo,nInicio + 1280,nTopo + 0050,nInicio + 1280)//Qtd
			oPrint:Line(nTopo,nInicio + 1420,nTopo + 0050,nInicio + 1420)//Prc Unit
			oPrint:Line(nTopo,nInicio + 1620,nTopo + 0050,nInicio + 1620)//Prc Total
			oPrint:Line(nTopo,nInicio + 1870,nTopo + 0050,nInicio + 1870)//IPI
			oPrint:Line(nTopo,nInicio + 1960,nTopo + 0050,nInicio + 1960)//ICMS
			oPrint:Line(nTopo,nInicio + 2080,nTopo + 0050,nInicio + 2080)//Dt Entreg
			
			// Texto    
			oPrint:Say(nTopo + 0007,nInicio + 0060,"Item"				   		,oFont3,100,,,0)
			oPrint:Say(nTopo + 0007,nInicio + 0170,"Produto"					,oFont3,100,,,0)
			oPrint:Say(nTopo + 0007,nInicio + 0470,"NCM"					,oFont3,100,,,0)
			oPrint:Say(nTopo + 0007,nInicio + 0690,"Descrição do Produto"		,oFont3,100,,,0)
			oPrint:Say(nTopo + 0007,nInicio + 1200,"UM"							,oFont3,100,,,0)
			oPrint:Say(nTopo + 0007,nInicio + 1300,"Qtd."						,oFont3,100,,,0)
			oPrint:Say(nTopo + 0007,nInicio + 1440,"Prc Unit."					,oFont3,100,,,0)
			oPrint:Say(nTopo + 0007,nInicio + 1640,"Prc Total"					,oFont3,100,,,0)
			oPrint:Say(nTopo + 0007,nInicio + 1890,"IPI"	  					,oFont3,100,,,0)
			oPrint:Say(nTopo + 0007,nInicio + 1980,"ICMS"	  					,oFont3,100,,,0)
			oPrint:Say(nTopo + 0007,nInicio + 2100,"Data Entrega"				,oFont3,100,,,0)
			nTopo += 0050 		
		EndIf		
		
		oPrint:Say(nTopo + 0007,nInicio + 0060,cAlias->UB_ITEM 						,oFont2,100,,,0)
		oPrint:Say(nTopo + 0007,nInicio + 0170,cAlias->UB_PRODUTO															,oFont2,100,,,0)
		oPrint:Say(nTopo + 0007,nInicio + 0470,cAlias->B1_POSIPI															,oFont2,100,,,0)
		oPrint:Say(nTopo + 0007,nInicio + 0690,substr(cAlias->B1_DESC,1,30)													,oFont2,100,,,0)
		oPrint:Say(nTopo + 0007,nInicio + 1200,cAlias->UB_UM																,oFont2,100,,,0)
		oPrint:Say(nTopo + 0007,nInicio + 1410,Alltrim(Transform(cAlias->UB_QUANT,PesqPict('SUB', 'UB_QUANT')))				,oFont2,100,,,1)
		oPrint:Say(nTopo + 0007,nInicio + 1610,Alltrim(Transform(cAlias->UB_VRUNIT,PesqPict('SUB', 'UB_VRUNIT')))			,oFont2,100,,,1)
		oPrint:Say(nTopo + 0007,nInicio + 1860,Alltrim(Transform(cAlias->UB_VLRITEM,PesqPict('SUB', 'UB_VLRITEM')))			,oFont2,100,,,1)
		oPrint:Say(nTopo + 0007,nInicio + 1950,str(MaFisRet(nItem,"IT_ALIQIPI"),4,1)	  																	,oFont2,100,,,1)
		oPrint:Say(nTopo + 0007,nInicio + 2070,str(MaFisRet(nItem,"IT_ALIQICM"),4,1)	  																	,oFont2,100,,,1)
		If cAlias->UB_ZZDISPO == 'S'
			oPrint:Say(nTopo + 0007,nInicio + 2100,DTOC(cAlias->UB_DTENTRE)														,oFont2,100,,,0)
		Else
			oPrint:Say(nTopo + 0007,nInicio + 2100,"VIDE OBS."													,oFont2,100,,,0)
		EndIf
		nPulo := 0050	
		
		nTamDesc := len(alltrim(cAlias->B1_DESC))/30				
		If nTamDesc > 1		
			For nZ	:= 1 to Int(nTamDesc-1) + 1 			
				oPrint:Say(nTopo + 0007 + nPulo,nInicio + 0690,substr(cAlias->B1_DESC,(nZ*30)+1,30)							,oFont2,100,,,0)
				nPulo += 0050				
			Next
		EndIf
		
		If cAlias->UB_ZZQNAEM != 0
			oPrint:Say(nTopo + nPulo + 0007,nInicio + 0690,allTrim(cAlias->UB_ZZEMBAL)+" - "+allTrim(Transform(cAlias->UB_ZZQNAEM, "999,999.99"))+" "+allTrim(cAlias->B1_ZZUMEMB),oFont2,100,,,0)
		Else
			oPrint:Say(nTopo + nPulo + 0007,nInicio + 0690,allTrim(cAlias->UB_ZZEMBAL)+" "+allTrim(cAlias->B1_ZZUMEMB),oFont2,100,,,0)
		EndIf

		nPulo += 0050	
		
		If !empty(cAlias->UB_NUMPCOM)
			oPrint:Say(nTopo + nPulo + 0007,nInicio + 0690,"Pedido: " + allTrim(cAlias->UB_NUMPCOM)+"/"+allTrim(cAlias->UB_ITEMPC),oFont2,100,,,0)
			nPulo += 0050	
		EndIf
		
		If !empty(cAlias->UB_ZZCODCL)
			oPrint:Say(nTopo + nPulo + 0007,nInicio + 0690,"Prod. Cliente: " + allTrim(cAlias->UB_ZZCODCL),oFont2,100,,,0)
			nPulo += 0050	
		EndIf
		
		If !empty(cAlias->B1_ZZDESC2)
			nRef := 0050
			cRef := "Ref. " + allTrim(cAlias->B1_ZZDESC2)
			oPrint:Say(nTopo + 0007 + nRef,nInicio + 0170,substr(cRef,1,18),oFont2,100,,,0)
			
			nRef += 0050
			nTamDesc := len(alltrim(cRef))/18				
			If nTamDesc > 1		
				For nZ	:= 1 to Int(nTamDesc-1) + 1 			
					oPrint:Say(nTopo + 0007 + nRef,nInicio + 0170,substr(cRef,(nZ*18)+1,18)						,oFont2,100,,,0)
					nRef += 0050				
				Next
			EndIf
			
			If nRef > nPulo
				nPulo := nRef
			EndIf
		EndIf
		
		
		oPrint:Box(nTopo, nInicio + 0040, nTopo + nPulo, nInicio + 2360)
		//Linhas Verticais
		oPrint:Line(nTopo,nInicio + 0040,nTopo + nPulo,nInicio + 0040)//Item
		oPrint:Line(nTopo,nInicio + 0150,nTopo + nPulo,nInicio + 0150)//Produto
		oPrint:Line(nTopo,nInicio + 0450,nTopo + nPulo,nInicio + 0450)//NCM
		oPrint:Line(nTopo,nInicio + 0670,nTopo + nPulo,nInicio + 0670)//Descri
		oPrint:Line(nTopo,nInicio + 1180,nTopo + nPulo,nInicio + 1180)//UM
		oPrint:Line(nTopo,nInicio + 1280,nTopo + nPulo,nInicio + 1280)//Qtd
		oPrint:Line(nTopo,nInicio + 1420,nTopo + nPulo,nInicio + 1420)//Prc Unit
		oPrint:Line(nTopo,nInicio + 1620,nTopo + nPulo,nInicio + 1620)//Prc Total
		oPrint:Line(nTopo,nInicio + 1870,nTopo + nPulo,nInicio + 1870)//IPI
		oPrint:Line(nTopo,nInicio + 1960,nTopo + nPulo,nInicio + 1960)//ICMS
		oPrint:Line(nTopo,nInicio + 2080,nTopo + nPulo,nInicio + 2080)//Dt Entreg
		
		nTopo += nPulo 
		
		nTotPed +=		cAlias->UB_VLRITEM
		cAlias->(dbskip())		
	EndDo
	
	
	If nTopo >= 2700
		oPrint:Line(nTopo + 0040,nInicio + 0040,nTopo + 0040,nInicio + 2360)
		oPrint:EndPage()
		oPrint:StartPage()			
		//Define o modo de Impressão como Retrato
		oPrint:SetPortrait()   //SetLandscape -> Para definir como modo Paisagem			
		//Funcao para impressao do Cabecalho
		IMPCAB()
		nTopo := 0450
	EndIf
	oPrint:Box(nTopo + 0060, nInicio + 0480, nTopo + 0110, nInicio + 1300)
	oPrint:Line(nTopo + 0060, nInicio + 0970,nTopo + 0110,nInicio + 0970) 
	
	oPrint:Say(nTopo + 0067, nInicio + 0500, "Frete"		,oFont3,100,,,0)
	oPrint:Say(nTopo + 0067, nInicio + 1280, " " + Alltrim(Transform(SUA->UA_FRETE,PesqPict('SUB', 'UB_VLRITEM')))	,oFont3,100,,,1)
    
	oPrint:Box(nTopo + 0110, nInicio + 0480, nTopo + 0160, nInicio + 1300)
	oPrint:Line(nTopo + 0110, nInicio + 0970,nTopo + 0160,nInicio + 0970) 
	
	oPrint:Say(nTopo + 0117, nInicio + 0500, "Outras Despesas"		,oFont3,100,,,0)
	oPrint:Say(nTopo + 0117, nInicio + 1280, " " + Alltrim(Transform(SUA->UA_DESPESA,PesqPict('SUB', 'UB_VLRITEM')))	,oFont3,100,,,1)
	
		
	//Valor IPI
	oPrint:Box(nTopo + 0060, nInicio + 1320, nTopo + 0110, nInicio + 1920)
	oPrint:Line(nTopo + 0060, nInicio + 1620,nTopo + 0110,nInicio + 1620) 
	
	oPrint:Say(nTopo + 0067, nInicio + 1340, "Valor IPI"		,oFont3,100,,,0)
	oPrint:Say(nTopo + 0067, nInicio + 1900, " " + Alltrim(Transform(MaFisRet(,"NF_VALIPI"),PesqPict('SUB', 'UB_VLRITEM')))	,oFont3,100,,,1)
		
	//Valor ICMS
	oPrint:Box(nTopo + 0110, nInicio + 1320, nTopo + 0160, nInicio + 1920)
	oPrint:Line(nTopo + 0110, nInicio + 1620,nTopo + 0160,nInicio + 1620) 
	
	oPrint:Say(nTopo + 0117, nInicio + 1340, "Valor ICMS"		,oFont3,100,,,0)
	oPrint:Say(nTopo + 0117, nInicio + 1900, " " + Alltrim(Transform(MaFisRet(,"NF_VALICM"),PesqPict('SUB', 'UB_VLRITEM')))	,oFont3,100,,,1)
	
	//Valor ICMS ST
	
	oPrint:Box(nTopo + 0160, nInicio + 1320, nTopo + 0210, nInicio + 1920)
	oPrint:Line(nTopo + 0160, nInicio + 1620,nTopo + 0210,nInicio + 1620) 
	
	oPrint:Say(nTopo + 0167, nInicio + 1340, "Valor ICMS ST"		,oFont3,100,,,0)
	oPrint:Say(nTopo + 0167, nInicio + 1900, " " + Alltrim(Transform(MaFisRet(,"NF_VALSOL"),PesqPict('SUB', 'UB_VLRITEM')))	,oFont3,100,,,1)
		
	//Box e Dados - Valor Total
	oPrint:Box(nTopo + 0210, nInicio + 1320, nTopo + 0260, nInicio + 1920)
	oPrint:Line(nTopo + 0210, nInicio + 1620,nTopo + 0260,nInicio + 1620) 
	
	oPrint:Say(nTopo + 0217, nInicio + 1340, "Valor Total Ped."		,oFont3,100,,,0)
	oPrint:Say(nTopo + 0217, nInicio + 1900, " " + Alltrim(Transform(nTotPed,PesqPict('SUB', 'UB_VLRITEM')))	,oFont3,100,,,1)
	    
	oPrint:Box(nTopo + 0260, nInicio + 1320, nTopo + 0310, nInicio + 1920)
	oPrint:Line(nTopo + 0260, nInicio + 1620,nTopo + 0310,nInicio + 1620) 
	
	nTot := nTotPed+MaFisRet(,"NF_VALIPI")+MaFisRet(,"NF_VALSOL")+SUA->UA_DESPESA+SUA->UA_FRETE
	oPrint:Say(nTopo + 0267, nInicio + 1340, "Valor Total"		,oFont3,100,,,0)
	oPrint:Say(nTopo + 0267, nInicio + 1900, " " + Alltrim(Transform(nTot,PesqPict('SUB', 'UB_VLRITEM')))	,oFont3,100,,,1)
		
	MaFisClear()
	cAlias->(dbCloseArea())
	nTopo += 0300
	If nTopo >= 2000
		oPrint:Line(nTopo + 0040,nInicio + 0040,nTopo + 0040,nInicio + 2360)
		oPrint:EndPage()
		oPrint:StartPage()			
		//Define o modo de Impressão como Retrato
		oPrint:SetPortrait()   //SetLandscape -> Para definir como modo Paisagem			
		//Funcao para impressao do Cabecalho
		IMPCAB()														
	EndIf
Return


Static Function IMPROD()
	Local nInicio    	:= 0000  // Indica a posicao da primeira colun
	Local aOBS			:= {}
	
	oPrint:Box(Mm2Pix(oPrint, oPrint:nVertSize()) - 1200, nInicio + 0040, Mm2Pix(oPrint, oPrint:nVertSize()) - 550, nInicio + 2360)
	oPrint:Say(Mm2Pix(oPrint, oPrint:nVertSize()) - 1200, nInicio + 0060,"Observações:",oFont3,100,,,0)
		
	nAux	:= 1200	
	
	//cVar := MSMM(,,,SUA->UA_ZZOBS)   
	For nY := 1 to MlCount(SUA->UA_ZZOBS) 
		aadd(aOBS,alltrim(MSMM(,,nY,SUA->UA_ZZOBS)))
	Next
	aadd(aOBS,"No caso de defeito no produto, a troca deverá ser solicitada em até 60 dias da compra. O comprador se declara ciente de que em nenhuma situação a vendedora arcará com") 
	aadd(aOBS,"indenização por dano moral, dano material ou lucros cessantes, limitando-se  à indenização à reposição da mercadoria comprada ou o valor equivalente.")	
	For nR := 1 to len(aOBS)
		nAux -= 0040
		oPrint:Say(Mm2Pix(oPrint, oPrint:nVertSize()) - nAux, nInicio + 0080,aOBS[nR],oFont2,100,,,0)	
	Next	


	// Box Condicoes Gerais	
	oPrint:Box(Mm2Pix(oPrint, oPrint:nVertSize()) - 0550, nInicio + 0040, Mm2Pix(oPrint, oPrint:nVertSize()) - 0070, nInicio + 2360)
	
	// Titulo Centralizado
	oPrint:Say (Mm2Pix(oPrint, oPrint:nVertSize()) - 0530, Mm2Pix(oPrint, oPrint:nHorzSize()) / 2,"Informações Gerais"		,oFont1,100,,,2)
		
	oPrint:Say(Mm2Pix(oPrint, oPrint:nVertSize()) - 0480, nInicio + 0060,"Validade"  						  		,oFont3,100,,,0)
	oPrint:Say(Mm2Pix(oPrint, oPrint:nVertSize()) - 0480, nInicio + 0450,": "+dToC(SUA->UA_DTLIM),oFont5,100,,,0)
		
	oPrint:Say(Mm2Pix(oPrint, oPrint:nVertSize()) - 0480, Mm2Pix(oPrint, oPrint:nHorzSize()) / 2 - 200,"Forma de Pagamento"  						  		,oFont3,100,,,0)
	oPrint:Say(Mm2Pix(oPrint, oPrint:nVertSize()) - 0480, Mm2Pix(oPrint, oPrint:nHorzSize()) / 2 + 0200,": " + SUA->UA_CONDPG + " - " + posicione("SE4",1,xFilial("SE4")+SUA->UA_CONDPG,"E4_DESCRI")	,oFont5,100,,,0)			
	
	aTemp		:= RetSX3Box(GetSX3Cache("UA_ZZAPLIC","X3_CBOX"),,,1)
	aCombo		:= {}	
	For nG := 1 to len(aTemp)
		aadd(aCombo,{aTemp[nG][2],aTemp[nG][3]})
	Next
	
	nPos		:= AScan(aCombo, {|aX| aX[1] == SUA->UA_ZZAPLIC})
	If nPos > 0
		cDestC := aCombo[nPos][2]	
	EndIf
	
	oPrint:Say(Mm2Pix(oPrint, oPrint:nVertSize()) - 0430, nInicio + 0060,"Material destinado a"    ,oFont3,100,,,0)
	oPrint:Say(Mm2Pix(oPrint, oPrint:nVertSize()) - 0430, nInicio + 0450,": " + cDestC,oFont5,100,,,0)
		
	oPrint:Say(Mm2Pix(oPrint, oPrint:nVertSize()) - 0430, Mm2Pix(oPrint, oPrint:nHorzSize()) / 2 - 200,"Ordem de Compra"    ,oFont3,100,,,0)
	oPrint:Say(Mm2Pix(oPrint, oPrint:nVertSize()) - 0430, Mm2Pix(oPrint, oPrint:nHorzSize()) / 2 + 0200,": " + SUA->UA_ZZPEDCL,oFont5,100,,,0)
	
	oPrint:Say(Mm2Pix(oPrint, oPrint:nVertSize()) - 0380, nInicio + 0060,"Natureza da Operacao"    ,oFont3,100,,,0)
	oPrint:Say(Mm2Pix(oPrint, oPrint:nVertSize()) - 0380, nInicio + 0450,": " + posicione("SF4",1,xFilial("SF4")+cTES,"F4_FINALID"),oFont5,100,,,0)
	
	/*	
	If SUA->UA_PROSPEC
		cTipo := Posicione("SUS",1,xFilial("SUS") + SUA->UA_CLIENTE + SUA->UA_LOJA,"US_TIPO")
	Else		
		cTipo := Posicione("SA1",1,xFilial("SA1") + SUA->UA_CLIENTE + SUA->UA_LOJA,"A1_TIPO")
	Endif	
	*/

	cTipo := SUA->UA_TIPOCLI
	Do Case
	  	Case cTipo == "F"
	  		cTipo := "Cons.Final"
	  	Case cTipo == "L"
	  		cTipo := "Produtor Rural"
	  	Case cTipo == "R"
	  		cTipo := "Revendedor"
	  	Case cTipo == "S"
	  		cTipo := "Solidario"
	  	Case cTipo == "X"
	  		cTipo := "Exportação"		     		
	EndCase		
	oPrint:Say(Mm2Pix(oPrint, oPrint:nVertSize()) - 0330, nInicio + 0060,"Tp. Cliente"    ,oFont3,100,,,0)	
	//oPrint:Say(Mm2Pix(oPrint, oPrint:nVertSize()) - 0330, nInicio + 0450,": "+cTipo ,oFont5,100,,,0)
	
	//oPrint:Say(Mm2Pix(oPrint, oPrint:nVertSize()) - 0330, Mm2Pix(oPrint, oPrint:nHorzSize()) / 2 - 200,"Amostra"    ,oFont3,100,,,0)	
	//oPrint:Say(Mm2Pix(oPrint, oPrint:nVertSize()) - 0330, Mm2Pix(oPrint, oPrint:nHorzSize()) / 2 + 0200,": "+IIF(SUA->UA_ZZAMOST == "S","Sim","Não") ,oFont5,100,,,0)
				
	dbSelectArea("SA4")
	dbSetOrder(1)
	dbSeek(xFilial("SA4")+SUA->UA_TRANSP)
	cTransp := ": " + "(" + SA4->A4_COD + ") " + alltrim(SA4->A4_NOME) +" - "+ alltrim(SA4->A4_MUN) +" - "+ alltrim(SA4->A4_EST) 	
	cTransp += " - ("+ alltrim(SA4->A4_DDD) +") " + Transform(SA4->A4_TEL,"@r 9999-9999")
	oPrint:Say(Mm2Pix(oPrint, oPrint:nVertSize()) - 0280, nInicio + 0060,"Transportadora"    ,oFont3,100,,,0)
	oPrint:Say(Mm2Pix(oPrint, oPrint:nVertSize()) - 0280, nInicio + 0450,cTransp,oFont5,100,,,0)
	oPrint:Say(Mm2Pix(oPrint, oPrint:nVertSize()) - 0230, nInicio + 0060,"Tp. Frete"    ,oFont3,100,,,0)
	If AllTrim(SUA->UA_TPFRETE) = "C"  							
		oPrint:Say(Mm2Pix(oPrint, oPrint:nVertSize()) - 0230, nInicio + 0450,": CIF"	,oFont5,100,,,0)
	Else
		oPrint:Say(Mm2Pix(oPrint, oPrint:nVertSize()) - 0230, nInicio + 0450,": FOB"	,oFont5,100,,,0)
	EndIf
		
	
	If !empty(SUA->UA_ZZREDES)
		dbSelectArea("SA4")
		dbSetOrder(1)
		dbSeek(xFilial("SA4")+SUA->UA_ZZREDES)
		cRedesp := ": " + "(" + SA4->A4_COD + ") " + alltrim(SA4->A4_NOME) +" - "+ alltrim(SA4->A4_MUN) +" - "+ alltrim(SA4->A4_EST) 
		cRedesp += " - ("+ alltrim(SA4->A4_DDD) +") " + Transform(SA4->A4_TEL,"@r 9999-9999")
		oPrint:Say(Mm2Pix(oPrint, oPrint:nVertSize()) - 0180, nInicio + 0060,"Redespacho"    ,oFont3,100,,,0)
		oPrint:Say(Mm2Pix(oPrint, oPrint:nVertSize()) - 0180, nInicio + 0450,cRedesp,oFont5,100,,,0)
		oPrint:Say(Mm2Pix(oPrint, oPrint:nVertSize()) - 0130, nInicio + 0060,"Tp. Frete"    ,oFont3,100,,,0)
		If AllTrim(SUA->UA_ZZTPFRE) = "C"  							
			oPrint:Say(Mm2Pix(oPrint, oPrint:nVertSize()) - 0130, nInicio + 0450,": CIF"	,oFont5,100,,,0)
		Else
			oPrint:Say(Mm2Pix(oPrint, oPrint:nVertSize()) - 0130, nInicio + 0450,": FOB"	,oFont5,100,,,0)
		EndIf
				
	EndIf	
	
	oPrint:Say(Mm2Pix(oPrint, oPrint:nVertSize()) - 0180, 2100,"_________________________"    ,oFont3,100,,,2)
	oPrint:Say(Mm2Pix(oPrint, oPrint:nVertSize()) - 0130, 2100,allTrim(Posicione("SU7",1,xFilial("SU7")+SUA->UA_OPERADO,"U7_NOME")),oFont3,100,,,2)
	
	oPrint:Say (Mm2Pix(oPrint, oPrint:nVertSize()) - 0060, nInicio + 0060,"SK-045",oFont2,100,,,2)
	oPrint:Say (Mm2Pix(oPrint, oPrint:nVertSize()) - 0060, Mm2Pix(oPrint, oPrint:nHorzSize()) / 2,'"Quem tem qualidade faz o presente e confia no futuro."',oFont5,100,,,2)
		
	oPrint:EndPage()
			
Return

Static Function Mm2Pix(oPrint, nMm)  
	/*Retirado metodo oPrint:nLogPixelX(), pois estava ocorrendo casos em clientes 
	em que a linha impressa que utliza a funcao MM2PIX saia desconfigurada*/
	//Local nValor := (nMm * oPrint:nLogPixelX()) / 25.4
	Local nValor := (nMm * 300) / 25.4
Return nValor  